apply plugin: 'com.android.application'
apply plugin: 'checkstyle'
apply plugin: 'findbugs'
apply plugin: 'pmd'

checkstyle {
    toolVersion '6.7'
}

android {
    packagingOptions {
        exclude 'META-INF/services/javax.annotation.processing.Processor'
    }
    lintOptions {
        warning 'InvalidPackage'
    }
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        applicationId "com.huhaoyu.tutu"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        multiDexEnabled true
    }
    buildTypes {
        debug {
            testCoverageEnabled = true
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main {
            res {
                srcDir 'src/main/res-umeng'
            }
        }
    }
}

task checkstyleMain(type: Checkstyle) {
    configFile file('../config/google_checks.xml')
    source 'src'
    include '**/*.java'

    classpath = files()
}

task findbugsMain(type: FindBugs) {
    classes = fileTree("$buildDir/intermediates/classes/debug")
    source = fileTree("src")
    classpath = files()
    effort = "max"
    reports {
        xml.enabled = false
        html.enabled = true
    }
    ignoreFailures = false
    excludeFilter = file("${project.rootDir}/config/findbugs-excludeFilter.xml")
}

task genCheckstyleHtml << {
    if (file("$buildDir/reports/checkstyle/main.xml").exists()) {
        ant.xslt(in: "$buildDir/reports/checkstyle/main.xml",
                style: "../config/checkstyle-noframes-severity-sorted.xsl",
                out: "$buildDir/reports/checkstyle/main.html")
    }
}

checkstyleMain.finalizedBy genCheckstyleHtml

findbugsMain.dependsOn "compileDebugSources"

task pmdMain(type: Pmd) {
    source fileTree('src')
    include '**/*.java'

    ruleSetFiles = files("../config/pmd_ruleset.xml")

    reports {
        xml.enabled = false
        html.enabled = true
    }

    ignoreFailures = false
}

check.finalizedBy findbugsMain
check.finalizedBy checkstyleMain
check.finalizedBy pmdMain

task release

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile project(':tufeedback')
    compile project(':thulib')
    testCompile 'junit:junit:4.12'
    compile files('libs/com.umeng.message.lib_v2.5.0.jar')
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.android.support:support-v4:23.1.1'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.github.rey5137:material:1.2.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.getbase:floatingactionbutton:1.10.1'
    compile 'com.wdullaer:materialdatetimepicker:2.0.2'
    compile 'jp.wasabeef:recyclerview-animators:2.1.0'
    compile 'com.rengwuxian.materialedittext:library:2.1.4'
    compile 'com.flipboard:bottomsheet-core:1.4.3'
    compile 'com.flipboard:bottomsheet-commons:1.4.3'
    compile 'com.android.support:multidex:1.0.1'
    compile 'com.umeng.analytics:analytics:latest.integration'
    compile('com.github.florent37:materialviewpager:1.1.3@aar') {
        transitive = true
    }
    debugCompile 'com.squareup.leakcanary:leakcanary-android:1.3.1'
    releaseCompile 'com.squareup.leakcanary:leakcanary-android-no-op:1.3.1'
}
